pragma solidity =0.6.6;

import '@uniswap/v2-core/contracts/interfaces/IUniswapV2Callee.sol';

import '../libraries/UniswapV2Library.sol';
import '../interfaces/V1/IUniswapV1Factory.sol';
import '../interfaces/V1/IUniswapV1Exchange.sol';
import '../interfaces/IUniswapV2Router01.sol';
import '../interfaces/IERC20.sol';
import '../interfaces/IWTRX.sol';

contract ExampleFlashSwap is IUniswapV2Callee {
    IUniswapV1Factory immutable factoryV1;
    address immutable factory;
    IWTRX immutable WTRX;

    constructor(
        address _factory,
        address _factoryV1,
        address router
    ) public {
        factoryV1 = IUniswapV1Factory(_factoryV1);
        factory = _factory;
        WTRX = IWTRX(IUniswapV2Router01(router).WTRX());
    }

    // needs to accept TRX from any V1 exchange and WTRX. ideally this could be enforced, as in the router,
    // but it's not possible because it requires a call to the v1 factory, which takes too much gas
    receive() external payable {}

    // gets tokens/WTRX via a V2 flash swap, swaps for the TRX/tokens on V1, repays V2, and keeps the rest!
    function uniswapV2Call(
        address sender,
        uint256 amount0,
        uint256 amount1,
        bytes calldata data
    ) external override {
        address[] memory path = new address[](2);
        uint256 amountToken;
        uint256 amountTRX;
        {
            // scope for token{0,1}, avoids stack too deep errors
            address token0 = IUniswapV2Pair(msg.sender).token0();
            address token1 = IUniswapV2Pair(msg.sender).token1();
            assert(msg.sender == UniswapV2Library.pairFor(factory, token0, token1)); // ensure that msg.sender is actually a V2 pair
            assert(amount0 == 0 || amount1 == 0); // this strategy is unidirectional
            path[0] = amount0 == 0 ? token0 : token1;
            path[1] = amount0 == 0 ? token1 : token0;
            amountToken = token0 == address(WTRX) ? amount1 : amount0;
            amountTRX = token0 == address(WTRX) ? amount0 : amount1;
        }

        assert(path[0] == address(WTRX) || path[1] == address(WTRX)); // this strategy only works with a V2 WTRX pair
        IERC20 token = IERC20(path[0] == address(WTRX) ? path[1] : path[0]);
        IUniswapV1Exchange exchangeV1 = IUniswapV1Exchange(factoryV1.getExchange(address(token))); // get V1 exchange

        if (amountToken > 0) {
            uint256 minTRX = abi.decode(data, (uint256)); // slippage parameter for V1, passed in by caller
            token.approve(address(exchangeV1), amountToken);
            uint256 amountReceived = exchangeV1.tokenToEthSwapInput(amountToken, minTRX, uint256(-1));
            uint256 amountRequired = UniswapV2Library.getAmountsIn(factory, amountToken, path)[0];
            assert(amountReceived > amountRequired); // fail if we didn't get enough TRX back to repay our flash loan
            WTRX.deposit{value: amountRequired}();
            assert(WTRX.transfer(msg.sender, amountRequired)); // return WTRX to V2 pair
            (bool success, ) = sender.call{value: amountReceived - amountRequired}(new bytes(0)); // keep the rest! (TRX)
            assert(success);
        } else {
            uint256 minTokens = abi.decode(data, (uint256)); // slippage parameter for V1, passed in by caller
            WTRX.withdraw(amountTRX);
            uint256 amountReceived = exchangeV1.trxToTokenSwapInput{value: amountTRX}(minTokens, uint256(-1));
            uint256 amountRequired = UniswapV2Library.getAmountsIn(factory, amountTRX, path)[0];
            assert(amountReceived > amountRequired); // fail if we didn't get enough tokens back to repay our flash loan
            assert(token.transfer(msg.sender, amountRequired)); // return tokens to V2 pair
            assert(token.transfer(sender, amountReceived - amountRequired)); // keep the rest! (tokens)
        }
    }
}
